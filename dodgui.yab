export sub addMap()															
	boxview 315,0 to 495,200,"mapBox","Map",2,"mainView"
	textedit 0,0 to 180,200,"mapView",0,"mapBox"
	textedit set "mapView","editable",false
	textedit color "mapView","bgcolor",0,0,0
	textedit color "mapView","textcolor",0,250,0
	textedit set "mapView","fixed","12"

end sub

export sub addScreen()
	textedit 0,0 to 310,380,"dodScreen",1,"mainView"
	textedit set "dodScreen","editable",false								// Can't edit
	textedit color "dodScreen","bgcolor",0,0,0								// Black Background
	textedit color "dodScreen","textcolor",0,250,0							// Green Text
	textedit set "dodScreen","fixed","12"
end sub

export sub addCommandLine()
	textcontrol 0,382 to 220,382+27,"command","Command:","","mainView"
end sub

export sub addStatus()
	boxview 315,210 to 460,355,"statusView","Status",2,"mainWindow"
end sub

export sub updateStatus(g,hitp,n$,lvl,key)
	draw flush "statusView"
	draw text 0,10,"Name: "+n$,"statusView"
	draw text 10,25,"Gold: "+str$(g),"statusView"
	draw text 10,40,"Hit-Points: "+str$(hitp),"statusView"
	draw text 10,55,"On Level: "+str$(lvl),"statusView"
	if key = true then
		draw text 10,70,"Have key: Yes","statusView"
	else
		draw text 10,70,"Have key: No ","statusView"
	endif
end sub

export sub openMainWindow()
	local winheight,winwidth
	window open 100,100 to 600,510,"mainWindow","Dungeon of Danger"	// Actual window
	window set "mainWindow","Flags","Not-Resizable"
	winheight = 700-100:winwidth=600-100
	view 0,0,500,410,"mainView","mainWindow"								// View on window
	addScreen()
	addMap()
	addCommandLine()
	addStatus()
	textcontrol set "command","focus","true"
end sub

export sub shutdown()
	window close "mainWindow"
end sub

export sub tprint(stuff$)
	textedit add "dodScreen",stuff$+"\n"
	numlines = textedit get "dodScreen","countphysicallines"
	if numlines > 25 then
		textedit set "dodScreen","gotoline",numlines
	endif
end sub

export sub tprintn(stuff$)
	textedit add "dodScreen",stuff$
end sub

export sub tprintc(stuff$)
	local tlen,s,m$,i
	
	tlen = len(stuff$)
	s = 20-(tlen/2)
	for i = 1 to s
		m$=m$+" "
	next i
	m$=m$+stuff$
	tprint(m$)
end sub

export sub mprint(map$)
	textedit add "mapView",map$+"\n"
end sub

export sub mprintn(map$)
	textedit add "mapView",map$
end sub

export sub mprintc(map$)
	local tlen,s,m$,i,w
	
	w=24
	tlen = len(map$)
	s = (w/2)-(tlen/2)
	print("w = "+str$(w)+" tlen= "+str$(tlen)+" s= "+str$(s))
	for i = 1 to s
		m$=m$+" "
	next i
	m$=m$+map$
	mprint(map$)
end sub
	
export sub cls()
	textedit clear "dodScreen"
end sub

export sub clm()
	textedit clear "mapView"
end sub

export sub clt()
	textcontrol clear "command"
end sub

export sub tinput$(mess$)
	local looper,ans$,nCommands,everyCommand,debug
	dim msg$(1)
	
	debug	= true
	looper	= true
	
	tprintn(mess$)
	
	while (looper = true)
		nCommands = token(message$,msg$(),"|")
		
		for everyCommand = 1 to nCommands
			if left$(msg$(everyCommand),8) = "command:" then
				ans$ = mid$(msg$(everyCommand),9)
				clt()
				looper = false
			elsif instr(msg$(everyCommand),"_Quit") then
				shutdown()
				end
			elseif instr(msg$(everyCommand),"_Activated") then
				textcontrol set "command","focus","true"
			endif
			if debug then
				print msg$(everyCommand)+":"+ans$
			endif
		next everyCommand
	wend
	if len(ans$) = 1 then
		ans$ = upper$(ans$)
	endif
	return(ans$)
end sub
